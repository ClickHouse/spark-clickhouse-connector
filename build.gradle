/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "idea"
    id "org.scoverage" version "7.0.0" apply false
    id "org.sonarqube" version "3.4.0.2513"
    id "org.nosphere.apache.rat" version "0.7.1"
    id "com.github.maiflai.scalatest" version "0.32" apply false
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

project.ext {
    scala_binary_version = System.getProperty("scala_binary_version")
    scala_version = scala_binary_version == "2.12" ? project.getProperty("scala_212_version") : project.getProperty("scala_213_version")
}

repositories {
    maven { url = "$mavenCentralMirror" }
}

rat {
    verbose.set(true)
    exclude(
            "**/README.md",
            "CODE_OF_CONDUCT.md",
            "version.txt",
            "clickhouse-core/src/main/scala-2.12/scala/util/Using.scala",
            "**/.gitkeep",
            "docs/imgs",
            "site/**",
            ".python-version",
            // Gradle
            "gradle/wrapper/**",
            "gradlew*",
            "**/build/**",
            // IDEs
            "*.iml",
            "*.ipr",
            "*.iws",
            "*.idea/**",
            ".editorconfig"
    )
}

subprojects {
    apply plugin: "scala"
    apply plugin: "java-library"
    apply plugin: "org.scoverage"
    apply plugin: "com.github.maiflai.scalatest"

    repositories {
        maven { url = "$mavenCentralMirror" }
    }

    version = getProjectVersion()

    configurations.all {
        resolutionStrategy {
            force "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
            force "com.fasterxml.jackson.core:jackson-core:$jackson_version"
            force "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
            force "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
            force "com.fasterxml.jackson.module:jackson-module-paranamer:$jackson_version"
            force "com.fasterxml.jackson.module:jackson-module-scala_$scala_binary_version:$jackson_version"

            force "org.scala-lang.modules:scala-xml_$scala_binary_version:$scala_xml_version"
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        withSourcesJar()
    }

    compileScala {
        options.fork = true
        options.forkOptions.jvmArgs += ["-Xss8M"]
    }

    test {
        maxParallelForks = 1
        tags {
            exclude 'org.scalatest.tags.Slow'
        }
    }

    task slowTest(type: Test) {
        maxParallelForks = 1
        tags {
            include 'org.scalatest.tags.Slow'
        }
    }

    scoverage {
        scoverageVersion = "1.4.11"
        reportDir.set(file("${rootProject.buildDir}/reports/scoverage"))
        highlighting.set(false)
        minimumRate.set(0.0)
    }
}

project(':clickhouse-core') {
    apply plugin: "idea"
    apply plugin: "antlr"
    apply plugin: "java-test-fixtures"

    sourceSets.main.scala {
        srcDirs += "src/main/scala-$scala_binary_version"
    }

    configurations {
        api {
            // workaround for https://github.com/gradle/gradle/issues/820
            extendsFrom = extendsFrom.findAll { it != configurations.antlr }
        }
    }

    generateGrammarSource {
        maxHeapSize = "64m"
        arguments += ["-visitor", "-package", "xenon.clickhouse"]
    }

    test {
        classpath += files("$projectDir/src/testFixtures/conf")
    }

    dependencies {
        api "org.scala-lang:scala-library:$scala_version"

        antlr "org.antlr:antlr4:$antlr_version"
        api "org.antlr:antlr4-runtime:$antlr_version"

        api "org.slf4j:slf4j-api:$slf4j_version"
        api "org.apache.commons:commons-lang3:$commons_lang3_version"

        api "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
        api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
        api "com.fasterxml.jackson.module:jackson-module-scala_$scala_binary_version:$jackson_version"

        api("com.clickhouse:clickhouse-jdbc:$clickhouse_jdbc_version:all") { transitive = false }

        compileOnly "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_api_version"

        testFixturesApi "org.slf4j:slf4j-api:$slf4j_version"
        testFixturesApi "org.scalatest:scalatest_$scala_binary_version:$scalatest_version"
        testFixturesRuntimeOnly "com.vladsch.flexmark:flexmark-all:$flexmark_version"
        testFixturesApi "com.dimafeng:testcontainers-scala-scalatest_$scala_binary_version:$testcontainers_scala_version"
        testFixturesApi "com.dimafeng:testcontainers-scala-clickhouse_$scala_binary_version:$testcontainers_scala_version"

        testImplementation "org.slf4j:slf4j-log4j12:$slf4j_version"
    }
}

boolean isVersionFileExists() {
    return file("version.txt").exists()
}

String getVersionFromFile() {
    return file("version.txt").text.trim()
}

String getProjectVersion() {
    if (!isVersionFileExists())
        throw new IllegalStateException("Can not find version.txt")
    return getVersionFromFile()
}

if (System.getProperty("spark_binary_versions").split(",").contains("3.2")) {
    apply from: file("spark-3.2/build.gradle")
}

if (System.getProperty("spark_binary_versions").split(",").contains("3.3")) {
    apply from: file("spark-3.3/build.gradle")
}

apply from: "deploy.gradle"
