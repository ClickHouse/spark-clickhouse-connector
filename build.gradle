/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.scoverage" version "7.0.0" apply false
    id "com.google.protobuf" version "0.8.18" apply false
    id "org.nosphere.apache.rat" version "0.7.0"
    id "com.github.maiflai.scalatest" version "0.32" apply false
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

repositories {
    maven { url = "$mavenCentralMirror" }
}

rat {
    verbose.set(true)
    exclude(
            "README.md",
            "CODE_OF_CONDUCT.md",
            "clickhouse-core/src/main/scala-2.12/scala/util/Using.scala",
            "**/.gitkeep",
            "site/**",
            ".python-version",
            // Gradle
            "gradle/wrapper/**",
            "gradlew*",
            "**/build/**",
            // IDEs
            "*.iml",
            "*.ipr",
            "*.iws",
            "*.idea/**",
            ".editorconfig"
    )
}

subprojects {
    apply plugin: "scala"
    apply plugin: "java-library"
    apply plugin: "org.scoverage"
    apply plugin: "com.github.maiflai.scalatest"

    repositories {
        maven { url = "$mavenCentralMirror" }
    }

    version = getProjectVersion()

    archivesBaseName = "${project.name}_${scala_binary_version}"

    configurations.all {
        resolutionStrategy {
            force "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
            force "com.fasterxml.jackson.core:jackson-core:$jackson_version"
            force "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
            force "com.fasterxml.jackson.module:jackson-module-paranamer:$jackson_version"
            force "com.fasterxml.jackson.module:jackson-module-scala_$scala_binary_version:$jackson_version"

            force "org.scala-lang.modules:scala-xml_$scala_binary_version:$scala_xml_version"
            force "org.scala-lang:scala-compiler:$scala_version"
            force "org.scala-lang:scala-library:$scala_version"
            force "org.scala-lang:scala-reflect:$scala_version"
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        withSourcesJar()
    }

    compileScala {
        options.fork = true
        options.forkOptions.jvmArgs += ["-Xss8M"]
    }

    test {
        maxParallelForks = 1
    }

    scoverage {
        scoverageVersion = "1.4.11"
        reportDir.set(file("${rootProject.buildDir}/reports/scoverage"))
        highlighting.set(false)
        minimumRate.set(0.0)
    }
}

project(':clickhouse-core') {
    apply plugin: "antlr"
    apply plugin: "com.google.protobuf"

    sourceSets.main.scala {
        srcDirs += "src/main/scala-$scala_binary_version"
    }

    configurations {
        api {
            // workaround for https://github.com/gradle/gradle/issues/820
            extendsFrom = extendsFrom.findAll { it != configurations.antlr }
        }
    }

    generateGrammarSource {
        maxHeapSize = "64m"
        arguments += ["-visitor", "-package", "xenon.clickhouse"]
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:$protobuf_version"
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {}
            }
        }
    }

    dependencies {
        api "org.scala-lang:scala-library:$scala_version"

        antlr "org.antlr:antlr4:$antlr_version"
        api "org.antlr:antlr4-runtime:$antlr_version"

        api "org.slf4j:slf4j-api:$slf4j_version"
        api "org.apache.commons:commons-lang3:$commons_lang3_version"

        api "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
        api "com.fasterxml.jackson.module:jackson-module-scala_$scala_binary_version:$jackson_version"

        api("com.google.guava:guava:$guava_version") {
            // Guava has one dependency that is needed for linkage at runtime: com.google.guava:failureaccess
            exclude group: "com.google.code.findbugs", module: "jsr305"
            exclude group: "com.google.errorprone", module: "error_prone_annotations"
            exclude group: "com.google.guava", module: "listenablefuture"
            exclude group: "com.google.j2objc", module: "j2objc-annotations"
            exclude group: "org.checkerframework", module: "checker-qual"
        }
        api("io.grpc:grpc-stub:$grpc_version") {
            exclude group: "com.google.guava", module: "guava"
            exclude group: "com.google.code.findbugs", module: "jsr305"
            exclude group: "com.google.errorprone", module: "error_prone_annotations"
            exclude group: "org.codehaus.mojo", module: "animal-sniffer-annotations"
        }
        api("io.grpc:grpc-protobuf:$grpc_version") {
            exclude group: "com.google.guava", module: "guava"
            exclude group: "com.google.code.findbugs", module: "jsr305"
            exclude group: "com.google.errorprone", module: "error_prone_annotations"
            exclude group: "org.codehaus.mojo", module: "animal-sniffer-annotations"
        }
        api("io.grpc:grpc-netty-shaded:$grpc_version") {
            exclude group: "com.google.guava", module: "guava"
            exclude group: "com.google.android", module: "annotations"
            exclude group: "com.google.code.findbugs", module: "jsr305"
            exclude group: "com.google.errorprone", module: "error_prone_annotations"
            exclude group: "org.codehaus.mojo", module: "animal-sniffer-annotations"
        }

        testImplementation "org.scalatest:scalatest_$scala_binary_version:$scalatest_version"
        testRuntimeOnly("com.vladsch.flexmark:flexmark-all:$flexmark_version") {
            exclude group: "log4j", module: "log4j"
        }

        testRuntimeOnly "org.slf4j:log4j-over-slf4j:$slf4j_version"
        testRuntimeOnly "ch.qos.logback:logback-classic:$logback_version"
    }
}

project(':clickhouse-core-integration') {
    apply plugin: 'java-test-fixtures'

    dependencies {
        implementation "org.scala-lang:scala-library:$scala_version" // for scala plugin detect scala binary version

        testFixturesApi project(path: ":clickhouse-core-runtime", configuration: "shadow")

        testFixturesApi "org.slf4j:slf4j-api:$slf4j_version"
        testFixturesApi "org.scalatest:scalatest_$scala_binary_version:$scalatest_version"
        testFixturesRuntimeOnly("com.vladsch.flexmark:flexmark-all:$flexmark_version") {
            exclude group: "log4j", module: "log4j"
        }
        testFixturesApi "com.dimafeng:testcontainers-scala-scalatest_$scala_binary_version:$testcontainers_scala_version"
        testFixturesApi "com.dimafeng:testcontainers-scala-clickhouse_$scala_binary_version:$testcontainers_scala_version"
        testFixturesRuntimeOnly "ru.yandex.clickhouse:clickhouse-jdbc:$clickhouse_jdbc_version"

        testRuntimeOnly "org.slf4j:log4j-over-slf4j:$slf4j_version"
        testRuntimeOnly "ch.qos.logback:logback-classic:$logback_version"
    }

    test {
        classpath += files("$projectDir/src/testFixtures/conf")
    }
}

project(":clickhouse-core-runtime") {
    apply plugin: "com.github.johnrengelman.shadow"

    tasks.jar.dependsOn tasks.shadowJar

    dependencies {
        compileOnly "org.scala-lang:scala-library:$scala_version"

        implementation(project(":clickhouse-core")) {
            exclude group: "org.scala-lang", module: "scala-library"
            exclude group: "org.slf4j", module: "slf4j-api"
        }
    }

    shadowJar {
        zip64 true
        classifier null

        exclude "google/**/*.proto"
        exclude "META-INF/native/**"
        exclude "META-INF/native-image/**"

        relocate "org.antlr.v4.runtime", "xenon.relocated.org.antlr.v4.runtime"
        relocate "org.apache.commons", "xenon.relocated.org.apache.commons"
        relocate "com.fasterxml.jackson", "xenon.relocated.com.fasterxml.jackson"
        relocate "com.google", "xenon.relocated.com.google"
        relocate "com.thoughtworks.paranamer", "xenon.relocated.com.thoughtworks.paranamer"
        relocate "io.grpc", "xenon.relocated.io.grpc"
        relocate "io.perfmark", "xenon.relocated.io.perfmark"

        mergeServiceFiles()
    }

    jar {
        archiveClassifier.set('empty')
    }
}

project(":clickhouse-spark-32") {
    dependencies {
        api project(":clickhouse-core")

        compileOnly("org.apache.spark:spark-sql_$scala_binary_version:$spark_version") {
            exclude group: "org.slf4j", module: "slf4j-log4j12" // use logback instead
            exclude group: "log4j", module: "log4j"
        }

        testImplementation("org.apache.spark:spark-sql_$scala_binary_version:$spark_version") {
            exclude group: "org.slf4j", module: "slf4j-log4j12" // use logback instead
            exclude group: "log4j", module: "log4j"
        }

        testImplementation "org.scalatest:scalatest_$scala_binary_version:$scalatest_version"
        testRuntimeOnly("com.vladsch.flexmark:flexmark-all:$flexmark_version") {
            exclude group: "log4j", module: "log4j"
        }

        testRuntimeOnly "org.slf4j:log4j-over-slf4j:$slf4j_version"
        testRuntimeOnly "ch.qos.logback:logback-classic:$logback_version"
    }
}

project(":clickhouse-spark-32-integration") {
    dependencies {
        implementation "org.scala-lang:scala-library:$scala_version" // for scala plugin detect scala binary version

        testImplementation project(path: ":clickhouse-spark-32-runtime", configuration: "shadow")
        testImplementation(testFixtures(project(":clickhouse-core-integration"))) {
            exclude module: "clickhouse-core-integration"
            exclude module: "clickhouse-core-runtime"
        }

        testImplementation("org.apache.spark:spark-sql_$scala_binary_version:$spark_version") {
            exclude group: "org.slf4j", module: "slf4j-log4j12" // use logback instead
            exclude group: "log4j", module: "log4j"
        }

        testImplementation("org.apache.spark:spark-sql_$scala_binary_version:$spark_version:tests") {
            exclude group: "org.slf4j", module: "slf4j-log4j12" // use logback instead
            exclude group: "log4j", module: "log4j"
        }

        testImplementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version")

        testRuntimeOnly "org.slf4j:log4j-over-slf4j:$slf4j_version"
        testRuntimeOnly "ch.qos.logback:logback-classic:$logback_version"
    }

    test {
        classpath += files("${project(':clickhouse-core-integration').projectDir}/src/testFixtures/conf")
    }
}

project(":clickhouse-spark-32-runtime") {
    apply plugin: "com.github.johnrengelman.shadow"

    tasks.jar.dependsOn tasks.shadowJar

    dependencies {
        compileOnly "org.scala-lang:scala-library:$scala_version"

        implementation(project(":clickhouse-spark-32")) {
            exclude group: "org.antlr", module: "antlr4-runtime"
            exclude group: "org.scala-lang", module: "scala-library"
            exclude group: "org.slf4j", module: "slf4j-api"
            exclude group: "org.apache.commons", module: "commons-lang3"
            exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
            exclude group: "com.fasterxml.jackson.module", module: "jackson-module-scala_$scala_binary_version"
        }
    }

    shadowJar {
        zip64 true
        classifier null

        exclude "google/**/*.proto"
        exclude "META-INF/native/**"
        exclude "META-INF/native-image/**"

        relocate "com.google", "xenon.relocated.com.google"
        relocate "io.grpc", "xenon.relocated.io.grpc"
        relocate "io.perfmark", "xenon.relocated.io.perfmark"

        mergeServiceFiles()
    }

    jar {
        archiveClassifier.set('empty')
    }
}

boolean isVersionFileExists() {
    return file("version.txt").exists()
}

String getVersionFromFile() {
    return file("version.txt").text.trim()
}

String getProjectVersion() {
    if (isVersionFileExists())
        return getVersionFromFile()

    String baseVersion = new Date().format("yyyy.MM.dd")
    return project.hasProperty("release") ? baseVersion : "$baseVersion-SNAPSHOT"
}

apply from: "deploy.gradle"
