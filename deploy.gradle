/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

subprojects {
    apply plugin: "maven-publish"
    apply plugin: 'signing'
    apply plugin: 'com.gradleup.nmcp'
    afterEvaluate {
        publishing {
            if (project.name.contains("-runtime")) {
                // Determine the corresponding implementation project name
                def implProjectName = project.name.replace("-runtime", "")
                def implProject = project.parent.findProject(":${implProjectName}")

                tasks.register('sourceJar', Jar) {
                    archiveClassifier.set('sources')

                    if (implProject != null) {
                        // Include the sources from the implementation project
                        from implProject.sourceSets.main.allSource

                        // Also include the sources from clickhouse-core
                        def coreProject = project.parent.findProject(":clickhouse-core")
                        if (coreProject != null) {
                            // Safely declare dependency on generateGrammarSource task if it exists
                            if (coreProject.tasks.findByName('generateGrammarSource') != null) {
                                dependsOn coreProject.tasks.generateGrammarSource
                            }
                            from coreProject.sourceSets.main.allSource
                        }
                    } else {
                        // Fallback to an empty jar if implementation project was not found
                        from sourceSets.main.allSource
                    }

                    group 'build'
                }

                tasks.register('javadocJar', Jar) {
                    archiveClassifier.set('javadoc')

                    if (implProject != null) {
                        // Include the javadoc from the implementation project
                        dependsOn implProject.javadoc
                        from implProject.javadoc.destinationDir

                        // Optionally include javadoc from clickhouse-core
                        def coreProject = project.parent.findProject(":clickhouse-core")
                        if (coreProject != null) {
                            dependsOn coreProject.javadoc
                            from coreProject.javadoc.destinationDir
                        }
                    } else {
                        // Fallback to the current project's javadoc
                        dependsOn javadoc
                        from javadoc.destinationDir
                    }

                    group 'build'
                }

                artifacts {
                    archives sourceJar
                    archives javadocJar
                }
                publications {
                    shadow(MavenPublication) { publication ->
                        project.shadow.component(publication)
                        artifact sourceJar
                        artifact javadocJar

                        pom {
                            name = "Spark ClickHouse Connector"
                            url = "https://github.com/clickhouse/spark-clickhouse-connector"
                            description = "Spark ClickHouse Connector build on Apache Spark DataSourceV2 API."
                            developers {
                                developer {
                                    id = 'pan3793'
                                    name = 'Cheng Pan'
                                    email = 'chengpan@apache.org'
                                }
                            }
                            licenses {
                                license {
                                    name = "The Apache License, Version 2.0"
                                    url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                                    distribution = "repo"
                                }
                            }
                            scm {
                                url = "https://github.com/clickhouse/spark-clickhouse-connector.git"
                            }
                            issueManagement {
                                system = 'GitHub'
                                url = 'https://github.com/clickhouse/spark-clickhouse-connector/issues'
                            }
                        }
                    }
                }
            }
        }
        if (project.hasProperty('release') && project.name.contains("-runtime")) {
            signing {
                def signingKey = System.getenv("SIGNING_KEY")
                def signingPassword = System.getenv("SIGNING_PASSWORD")
                if (signingKey != null && signingPassword != null) {
                    useInMemoryPgpKeys(signingKey, signingPassword)
                    sign publishing.publications.shadow
                }
            }

            nmcp {
                publish('shadow') {
                    username = System.getenv('NMCP_USERNAME')
                    password = System.getenv('NMCP_PASSWORD')
                    publicationType = 'USER_MANAGED'
                }
            }        }
    }
}
