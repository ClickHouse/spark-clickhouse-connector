/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

subprojects {
    apply plugin: "maven-publish"
    apply plugin: 'signing'
    afterEvaluate {
        publishing {
            if (project.name.contains("-runtime")) {
                tasks.register('sourceJar', Jar) {
                    dependsOn classes
                    archiveClassifier.set('sources')
                    from sourceSets.main.allSource
                    group 'build'
                }

                tasks.register('javadocJar', Jar) {
                    dependsOn javadoc
                    archiveClassifier.set('javadoc')
                    from javadoc.destinationDir
                    group 'build'
                }

                artifacts {
                    archives sourceJar
                    archives javadocJar
                }
                publications {
                    shadow(MavenPublication) { publication ->
                        project.shadow.component(publication)
                        artifact sourceJar
                        artifact javadocJar

                        pom {
                            name = "Spark ClickHouse Connector"
                            url = "https://github.com/housepower/spark-clickhouse-connector"
                            description = "Spark ClickHouse Connector build on Apache Spark DataSourceV2 API."
                            developers {
                                developer {
                                    id = 'pan3793'
                                    name = 'Cheng Pan'
                                    email = 'chengpan@apache.org'
                                }
                            }
                            licenses {
                                license {
                                    name = "The Apache License, Version 2.0"
                                    url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                                    distribution = "repo"
                                }
                            }
                            scm {
                                url = "https://github.com/housepower/spark-clickhouse-connector.git"
                            }
                            issueManagement {
                                system = 'GitHub'
                                url = 'https://github.com/housepower/spark-clickhouse-connector/issues'
                            }
                        }
                    }
                }
            }

            repositories {
                maven {
                    allowInsecureProtocol = true
                    url = project.version.endsWith("-SNAPSHOT") ? "$mavenSnapshotsRepo" : "$mavenReleasesRepo"

                    credentials {
                        username = project.hasProperty("mavenUser") ? "$mavenUser" : ""
                        password = project.hasProperty("mavenPassword") ? "$mavenPassword" : ""
                    }
                }
            }
        }
        if (project.hasProperty('release') && project.name.contains("-runtime")) {
            signing {
                useGpgCmd()
                sign publishing.publications.shadow
            }
        }
    }
}
